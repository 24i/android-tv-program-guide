apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 30


    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}


def libraryGroupId = 'nl.npo'
def libraryArtifactId = 'tvguide'
def libraryVersion = '1.0.0'
group = libraryGroupId
version = libraryVersion


ext.mavenRepositoryUsername = findProperty('github_username') ?: "24i"
ext.mavenRepositoryPassword = findProperty('github_token') ?: System.getenv("GH_TOKEN")



dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.leanback:leanback:1.0.0"

    implementation "androidx.constraintlayout:constraintlayout:1.1.3"

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    api "com.jakewharton.threetenabp:threetenabp:1.2.2"
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

project.afterEvaluate {
    publishing {

        publications {
            aar(MavenPublication) {
                groupId libraryGroupId
                version libraryVersion
                artifactId libraryArtifactId

                artifact(bundleReleaseAar)
                artifact(sourcesJar)

                pom.withXml {
                    // for dependencies and exclusions
                    createDependenciesNode(asNode(), configurations)
                }
            }
        }

        repositories {
            maven {
                name "Artifactory"
                url "https://maven.pkg.github.com/24i/android-tv-program-guide"
                credentials {
                    username = "$mavenRepositoryUsername"
                    password = "$mavenRepositoryPassword"
                }
            }
        }
    }
}

